/**
 * 文字を描画するためのテクスチャ
 *
 * BitmapLayerTreeOwnerで描画するので、やや富豪的実装となっている。
 * ほぼ更新しないのであれば、Bitmap/ImageFunction/Font/Textureを組み合わせて、Textureを生成する方が良い。
 * もしくは、Layer利用が多いのであれば、LayerOwnerTextureにLayerをつなげて、そこで色々描画させた方がよい。
 * この実装は、従来のメッセージレイヤーを単一で使うことを実現するための実装。
 */
class MessageTexture extends LayerOwnerTexture {
	var textX_:int;
	var textY_:int;
	var x:int;
	var y:int;
	var opacity_:int;
	var visible_:bool;
	// メッセージレイヤーが一つと言うことで、ここにテクスチャ作っているが、
	// メッセージレイヤー複数作るのなら、テクスチャは1個だけ作る方が効率的。
	// TextureはCopyOnWriteされていない(忘れていた、将来対応した方が良さそう)
	var pageAnime_:AnimationTexture;
	var lineAnime_:AnimationTexture;

	function MessageTexture( width:int , height:int ) {
		super.LayerOwnerTexture( width, height );

		//primary_.font.face = "Noto Sans CJK JP,Noto Sans,MotoyaLMaru,Roboto";
		primary_.font.face = "Gen Shin Gothic Monospace";
		primary_.font.height = 32;

		pageAnime_ = new AnimationTexture("PageBreak", 16, 800, true );
		lineAnime_ = new AnimationTexture("LineBreak", 16, 800, true );

		textX_ = 0;
		textY_ = 0;
		x = 0;
		y = 0;
		opacity_ = 128;
		visible_ = true;
	}

	// function drawText(x, y, text, color, opa=255, aa=true, shadowlevel=0, shadowcolor=0x000000, shadowwidth=0, shadowofsx=0, shadowofsy=0) {}
	/**
	 * レイヤーに描画しているので、onDraw内関係なく描画できる。
	 * Canvasで描画するのであれば、onDraw内で描画しなければならず、内部で一度コマンドを溜めるなどする必要がある。
	 */
	function putText( text, color ) {
		var w = primary_.font.getEscWidthX( text );
		if( (w + textX_) > (width-10-primary_.font.height) ) {
			var h = primary_.font.height; // primary_.font.getEscWidthY( text );
			textY_ += h;
			textX_ = 0;
		}
		primary_.drawText( textX_, textY_, text, color );
		textX_ += w;
		cancelAnime();
	}
	function cancelAnime() {
		pageAnime_.stop();
		lineAnime_.stop();
	}
	function draw( canvas:Canvas ) {
		if( visible_ ) {
			update();
			canvas.save();
			canvas.blendMode = bmAlpha;
			// 下地(黒)を半透明で余白を含めて描画
			canvas.matrix.translate( x, y );
			canvas.fill( width+20, height+20, opacity_<<24 );
			canvas.restore();

			if( pageAnime_.isPlaying ) {
				pageAnime_.draw( canvas, textX_+10+x, textY_+10+y, primary_.font.height );
			} else if( lineAnime_.isPlaying ) {
				lineAnime_.draw( canvas, textX_+10+x, textY_+10+y, primary_.font.height );
			}

			canvas.save();
			// テキストレイヤーを余白分移動して描画
			canvas.matrix.translate( x+10, y+10 );
			canvas.drawTexture( texture_ );
			canvas.restore();
		}
	}
	function showLineBreak() {
		lineAnime_.start();
	}
	function showPageBreak() {
		pageAnime_.start();
	}
	function clear() {
		primary_.fillRect( 0, 0, width, height, 0 );
		textX_ = 0;
		textY_ = 0;
		cancelAnime();
	}
	function processReturn() {
		var h = primary_.font.height;
		textY_ += h;
		textX_ = 0;
		cancelAnime();
	}
	property width {
		getter() { return primary_.width; }
	}
	property height {
		getter() { return primary_.height; }
	}
	property visible {
		getter() { return visible_; }
		setter( x ) { visible_ = x; }
	}
};
